package chapter04.ruleAttributes;

import org.drools.devguide.eshop.model.Item;

global org.drools.devguide.eshop.service.EShopConfigService configService;

rule "Classify Item - Low Range"
    when
        $i: Item( cost <= 200, category == Category.NA )
    then
        $i.setCategory(Item.Category.LOW_RANGE);
        update($i);
end

rule "Classify Item - Mid Range"
    when
        $i: Item( cost > 200 && cost < 500, category == Category.NA )
    then
        $i.setCategory(Item.Category.MID_RANGE);
        update($i);
end

rule "Classify Item - High Range"
    when
        $i: Item( cost >= 500, category == Category.NA )
    then
        $i.setCategory(Item.Category.HIGH_RANGE);
        update($i);
end

rule "Classify Item - Mid/High Range (special)"
    no-loop
        salience($i.getCost())
    enabled (configService.isMidHighCategoryEnabled())
    when
        $i: Item( cost > 300 && cost < 400 )
    then
        $i.setCategory(Item.Category.SPECIAL_MIDHIGH_RANGE);
        update($i);
end
